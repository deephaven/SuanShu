plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

group = 'io.deephaven'
version = '0.1.1-SNAPSHOT'
description = 'Extension of original open-sourced math library, SuanShu.'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    // lots of javadoc problems, but need it for publishing to mavenCentral
    source = null
}

dependencies {
    api 'joda-time:joda-time:2.9.9'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    testImplementation 'junit:junit:4.10'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'io.deephaven:SuanShu'
                description = 'Extension of original open-sourced math library, SuanShu.'
                url = 'https://github.com/deephaven/SuanShu'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    url = 'https://github.com/deephaven/SuanShu/tree/main'
                    connection = 'scm:git:git://github.com/deephaven/SuanShu.git'
                    developerConnection = 'scm:git:ssh://github.com/deephaven/SuanShu.git'
                }
                developers {
                    developer {
                        id = 'devinrsmith'
                        name = 'Devin Smith'
                        email = 'devinsmith@deephaven.io'
                    }
                }

            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            // Will pick up ossrhUsername, ossrhPassword
            credentials(PasswordCredentials)

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
